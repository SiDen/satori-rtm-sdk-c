# vim:ft=cmake

message(STATUS "Reading credentials.json")
file(READ "${CREDENTIALS_FILE}" CREDENTIALS_JSON)

set(json_pos 0)
set(json_state 0)
string(LENGTH "${CREDENTIALS_JSON}" json_length)
while(${json_pos} LESS ${json_length})
  # Walk through file, checking each character

  string(SUBSTRING "${CREDENTIALS_JSON}" ${json_pos} 1 json_char)

  if("${json_char}" STREQUAL ",")
    # After a ",", a new key follows
    set(json_state 0)
  elseif("${json_char}" STREQUAL ":")
    # After a ":", a new value follows
    set(json_state 1)
  elseif("${json_char}" STREQUAL "\"")
    # Process strings
    set(json_eos ${json_pos})
    while(${json_eos} LESS ${json_length})
      math(EXPR json_eos "${json_eos}+1")
      string(SUBSTRING "${CREDENTIALS_JSON}" ${json_eos} 1 json_char)
      if(${json_char} STREQUAL "\\")
        math(EXPR json_eos "${json_eos}+1")
      elseif(${json_char} STREQUAL "\"")
        break()
      endif()
    endwhile()

    if(${json_char} STREQUAL "\"")
      # String found. Assign either key (in form of variable name) or value
      # (in form of variable assignment)
      math(EXPR json_pos "${json_pos} + 1")
      math(EXPR json_len "${json_eos} - ${json_pos}")
      string(SUBSTRING "${CREDENTIALS_JSON}" ${json_pos} ${json_len} json_substr)
      if(${json_state} EQUAL 0)
        set(json_key "JSON_CREDENTIALS_${json_substr}")
      else()
        set(${json_key} ${json_substr})
      endif()
    endif()

    set(json_pos ${json_eos})
  endif()

  math(EXPR json_pos "${json_pos}+1")
endwhile()

# Remap JSON names to C SDK definitions
if((NOT YOUR_ENDPOINT) AND JSON_CREDENTIALS_endpoint)
  set(YOUR_ENDPOINT ${JSON_CREDENTIALS_endpoint})
endif()
if(NOT YOUR_APPKEY AND JSON_CREDENTIALS_appkey)
  set(YOUR_APPKEY ${JSON_CREDENTIALS_appkey})
endif()
if(NOT YOUR_ROLE AND JSON_CREDENTIALS_auth_role_name)
  set(YOUR_ROLE ${JSON_CREDENTIALS_auth_role_name})
endif()
if(NOT YOUR_ROLE_SECRET AND JSON_CREDENTIALS_auth_role_secret_key)
  set(YOUR_ROLE_SECRET ${JSON_CREDENTIALS_auth_role_secret_key})
endif()
if(NOT YOUR_CHANNEL AND JSON_CREDENTIALS_auth_restricted_channel)
  set(YOUR_CHANNEL ${JSON_CREDENTIALS_auth_restricted_channel})
endif()
